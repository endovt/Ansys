! Proposta do exercício:
! Estabelecer relação dos casos de carga com tensões globais e locais.
! Efeito da quantidade de compartimentos nas tensões.

KEYW,PR_SGVOF,1         ! suppress window popup after /clear, KEYW,PR_SGVOF,0 to undo
/UIS,MSGPOP,3           ! message dialog box: only errors
/CLEAR

!* Geometry parameters
MULTIPRO,'START',9 ! number of design variables
    ! plate dimensions (shell)
    *CSET,1,3,comp_l,'Enter length (mm)',20000    
    *CSET,4,6,comp_w,'Enter width (mm)',3000      
    *CSET,7,9,comp_h,'Enter height (mm)',2000
    *CSET,10,12,comp_t,'Enter thickness (mm)',40 
    ! additional info
    *CSET,13,15,comp_pres,'Enter uniform pressure (MPa)',0.01
    *CSET,16,18,mat_e,'Elastic modulus (MPa)',200000
    *CSET,19,21,mesh_size,'Enter global mesh size (mm)',500
    ! 
    *CSET,22,24,comp_n,'Number of repetitions',1 
    *CSET,25,27,load_case,'Load case (1, 2, or 3)',1
    ! display message
    *CSET,61,62,'Define the design variables',''                ! message
    *CSET,63,64,'',''                                           ! message
MULTIPRO,'END'

! Constant values
*SET,mat_nu,0.3 ! poisson ratio

/PREP7
!* Geometry
BLC4,0,0,comp_l,comp_h,(comp_w/2)   ! solid block
! repetitions
*IF,comp_n,GT,1,THEN
    VGEN,comp_n,ALL,,,comp_l,,,,0   ! copy with offset
    VGLUE,ALL                       ! glue volumes
*ENDIF
! delete volumes and work only with areas
VDELE,ALL       ! delete all volumes
ASEL,S,LOC,Z,0  ! select areas at z=0
ADELE,ALL       ! delete selected areas
ALLSEL          ! select all

!* Mesh
ET,1,SHELL281           ! second-order shell element
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,mat_e      ! elastic modulus
MPDATA,PRXY,1,,mat_nu   ! poisson ratio
SECT,1,SHELL,,  
SECDATA,comp_t,1,0.0,3  ! shell thickness
SECOFFSET,MID           ! shell offset
SECCONTROL,,,,,,,
ESIZE,mesh_size 
AMESH,ALL

!* Boundary condition
! symmetry
NSEL,S,LOC,Z,0          ! select nodes
D,ALL,UZ,0,,,,ROTX,ROTY ! fix dof
! symmetry
NSEL,S,LOC,X,0          ! select nodes
D,ALL,UX,0,,,,ROTY,ROTZ ! fix dof

!* Load cases
*IF,load_case,EQ,1,THEN
    /COM,Load case: Global + Local
    ! Fix top central nodes
    NSEL,S,LOC,X,0                  ! select nodes
    NSEL,R,LOC,Y,comp_h             ! select nodes
    D,ALL,ALL
    ! Pressure bottom
    ASEL,S,LOC,Y,0                  ! select areas based on location
    ESLA,S                          ! select elements based on the selected areas
    SFE,ALL,1,PRES,,-comp_pres,,,   ! apply pressure on the selected elements
*ELSEIF,load_case,EQ,2
    /COM,Load case: Local
    ! Fix all top nodes  
    ASEL,U,LOC,Y,comp_h  
    NSLA,S,1
    NSEL,R,LOC,Y,comp_h ! select nodes
    D,ALL,ALL  
    ! Pressure bottom
    ASEL,S,LOC,Y,0  
    ESLA,S  
    SFE,ALL,1,PRES, ,-comp_pres, , ,
*ELSEIF,load_case,EQ,3
    /COM,Load case: Global
    ! Fix top central nodes
    NSEL,S,LOC,X,0 ! select nodes
    NSEL,R,LOC,Y,comp_h ! select nodes
    D,ALL,ALL
    ! Pressure top
    ASEL,S,LOC,Y,comp_h  
    ESLA,S  
    SFE,ALL,1,PRES, ,comp_pres, , ,
*ELSE
    /EXIT
*ENDIF

!* Solution
ALLSEL
/SOLU
SOLVE

!* Post-processor
FINISH  
/POST1  
SET,LAST
/EFACET,1   

! Maximum stress calculation (bottom, origin)
node_0 = node(0,0,0)
NSEL,S,,,node_0 
ESLN,S  
PLNSOL, S,X, 0,1.0  
*GET,max_stress,PLNSOL,0,MAX
/TITLE,%(2*comp_n)% comp. Load case %load_case%. SX_max (0,0,0) = %max_stress% MPa.
/AN3D,ANUM,0,   105, -1799.1    ,  952.55    ,  1247.3  
/AN3D,ARR2,  1.000, -345.450    ,  481.934    , 0.909495E-12,  0.00000    ,  0.00000    ,  0.00000  
/AN3D,TEXT, -1799.1    ,  952.55    ,  1247.3    ,SX=%max_stress% MPa 

! Expand symmetry
/EXPAND,2,RECT,HALF,0.00001,,,2,RECT,HALF,,0,/EXPAND,rep_y,RECT,FULL,,plate_w, ,RECT,FULL,,,,   

! Displacement plot
ALLSEL
PLNSOL, U,SUM, 0,1.0

*CREATE,usum,mac, ! create a file containing a macro
    /POST1
    PLNSOL, U,SUM, 0,1.0
*END
*ABBR,USUM,usum ! create a buttom (Toolbar shorcut)

*CREATE,sx,mac, ! create a file containing a macro
    /POST1
    PLNSOL, S,X, 0,1.0 ! plot sigma_x stress
*END
*ABBR,SX,sx ! create a buttom (Toolbar shorcut)