KEYW,PR_SGVOF,1         ! suppress window popup after /clear, KEYW,PR_SGVOF,0 to undo
/UIS,MSGPOP,3           ! message dialog box: only errors
/CLEAR

!* Define the design variables
MULTIPRO,'START',5 ! number of design variables
    ! plate dimensions (shell)
    *CSET,1,3,plate_l,'Enter plate length (mm)',3700    ! x-direction
    *CSET,4,6,plate_w,'Enter plate width (mm)',880      ! y-direction
    *CSET,7,9,plate_t,'Enter plate thickness (mm)',20   ! z-direction
    ! loading condition
    *CSET,10,12,plate_pres,'Enter uniform pressure (MPa)',0.25  ! pressure on the plate
    ! mesh parameter
    *CSET,13,15,mesh_size,'Enter global mesh size (mm)',50      ! mesh size
    ! display message
    *CSET,61,62,'Define the design variables',''                ! message
    *CSET,63,64,'',''                                           ! message
MULTIPRO,'END'

*IF,_BUTTON,EQ,1,THEN
    /EXIT ! or /EOF ?
*ENDIF

/PREP7
/UNITS,MPa

!* Geometry
RECTNG,0,(plate_l/2),0,(plate_w/2),   

!* Material
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,200000     ! elastic modulus
MPDATA,PRXY,1,,0.3      ! poisson ratio
MPDATA,DENS,1,,7.8e-9   ! density

!* Section
SECT,1,SHELL,,  
SECDATA,plate_t,1,0.0,3 
SECOFFSET,MID   
SECCONTROL,,,, , , ,

!* Mesh
ET,1,SHELL281       ! second-order shell
AESIZE,ALL,mesh_size   ! global mesh size
AMESH,ALL           ! generate mesh

!* Boundary conditions
DL,1,,SYMM      ! symm
DL,4,,SYMM      ! symm
LSEL,S,,,2,3    ! select lines (to be fixed)
DL,ALL,,ALL,    ! fix selected lines (all dof)
ALLSEL

!* Pressure (directly on elements)
SFA,ALL,1,PRESS,plate_pres  ! apply pressure on all areas

!* Solution
/SOLU
SOLVE
/POST1
SET,LAST

!* View 
/USER,  1   
/FOC,   1,   465.743304599    ,   399.123168456    ,   188.214214287
/VIEW,  1,  0.440532970185    , -0.813544009814    ,  0.379574559574
/ANG,   1,  -54.3391859892  
/AUTO,1 
/EFACET,1   
/ESHAPE,1                   ! show thickness
/EXPAND,4,POLAR,HALF,,90    ! symmetry expansion
/REPLOT 

!* Results
! Displacement
*CREATE,plot_u,mac, ! create a file containing a macro
    /POST1
    PLNSOL, U,SUM, 0,1.0
*END
*ABBR,PLOT_U,plot_u ! create a buttom (Toolbar shorcut)

! Sigma x
*CREATE,plot_sx,mac, ! create a file containing a macro
    /POST1
    PLNSOL, S,X, 0,1.0  
*END
*ABBR,PLOT_SX,plot_sx ! create a buttom (Toolbar shorcut)

! Sigma y
*CREATE,plot_sy,mac, ! create a file containing a macro
    /POST1
    PLNSOL, S,y, 0,1.0  
*END
*ABBR,PLOT_SY,plot_sy ! create a buttom (Toolbar shorcut)

!* Analysis info
*GET,e_count,ELEM,,COUNT ! obtain the number of elements
/TITLE,Geometry (%plate_l%x%plate_w%x%plate_t%) mm.  %e_count% elements. P=%plate_pres% MPa.

P1_node = node(plate_l/2,0,0) ! get node number based on a coordinate
*GET,P1_SX,NODE,P1_node,S,X   
*GET,P1_SY,NODE,P1_node,S,Y
P2_node = node(0,plate_w/2,0) ! get node number based on a coordinate
*GET,P2_SX,NODE,P2_node,S,X
*GET,P2_SY,NODE,P2_node,S,Y

!* Print stress
/COM,Stress results: Point P1 (top)
/COM,SX = %P1_SX% MPa, SY = %P1_SY% MPa
/COM,Stress results: Point P2 (top)
/COM,SX = %P2_SX% MPa, SY = %P2_SY% MPa