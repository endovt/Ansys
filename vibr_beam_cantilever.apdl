KEYW,PR_SGVOF,1         ! suppress window popup after /clear, KEYW,PR_SGVOF,0 to undo
/UIS,MSGPOP,3           ! message dialog box: only errors
/CLEAR

!* Define the design variables
MULTIPRO,'START',6 ! number of design variables
    ! plate dimensions (shell)
    *CSET,1,3,beam_l,'Enter beam length (mm)',1000 
    *CSET,4,6,beam_w,'Enter beam width (mm)',10
    *CSET,7,9,beam_h,'Enter beam height (mm)',20
    *CSET,10,12,mat_e,'Enter elastic modulus (MPa)',200000 
    *CSET,13,15,mat_rho,'Enter density (t/mm3)',7.8e-9 
    *CSET,16,18,mesh_size,'Enter global mesh size (mm)',100  
    ! display message
    *CSET,61,62,'Define the design variables',''                ! message
    *CSET,63,64,'',''                                           ! message
MULTIPRO,'END'

*IF,_BUTTON,EQ,1,THEN
    /EXIT ! or /EOF ?
*ENDIF

! Analytical reference
*SET,beam_a,beam_w*beam_h ! beam area
*SET,beam_i,((beam_w)*(beam_h**3))/12 ! beam moment inertia
*SET,pi,acos(-1)
! Frequency (table)
*SET,freq_k1,1.875
*SET,freq_k2,4.694
*SET,freq_k3,7.855
! Frequency
*SET,freq_n1,(1/(2*pi))*((freq_k1/beam_l)**2)*(((mat_e*beam_i)/(mat_rho*beam_a))**(1/2))
*SET,freq_n2,(1/(2*pi))*((freq_k2/beam_l)**2)*(((mat_e*beam_i)/(mat_rho*beam_a))**(1/2))
*SET,freq_n3,(1/(2*pi))*((freq_k3/beam_l)**2)*(((mat_e*beam_i)/(mat_rho*beam_a))**(1/2))

/PREP7
/UNITS,MPa
! geometry
K,1,0,0,0
K,2,beam_l,0,0
L,1,2
! material
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,mat_e 
MPDATA,PRXY,1,,0.3  
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,DENS,1,,mat_rho  
! element
ET,1,BEAM189
! section
SECTYPE,   1, BEAM, RECT, , 0   
SECOFFSET, CENT 
SECDATA,beam_h,beam_w,0,0,0,0,0,0,0,0,0,0   
! mesh
ESIZE,mesh_size
LMESH,ALL

FINISH  
/SOLU
ANTYPE,2 ! modal analysis
MODOPT,LANB,10  ! calculate 10 modes

! boundary conditions
D,ALL,UZ,0, , , ,ROTX,ROTY, , , ,  ! 2d problem
D,1,ALL,0 !fix node 1
SOLVE

/POST1
*GET,n_elements,ELEM,0,COUNT
*GET,freq_n1_ansys,MODE,1,FREQ
*GET,freq_n2_ansys,MODE,2,FREQ
*GET,freq_n3_ansys,MODE,3,FREQ

/EFACET,1 

! print results
*CREATE,freq_1,mac, ! create a file containing a macro
    /POST1
    SET,,, ,,, ,1  ! vibration mode
    PLNSOL, U,SUM, 1,1.0
    /TITLE, %n_elements% beam elements, error: %((freq_n1_ansys-freq_n1)/freq_n1)*100%
    /REPLOT
*END
*ABBR,freq_1,freq_1 ! create a buttom (Toolbar shorcut)

*CREATE,freq_2,mac, ! create a file containing a macro
    /POST1
    SET,,, ,,, ,2  ! vibration mode
    PLNSOL, U,SUM, 1,1.0
    /TITLE, %n_elements% beam elements, error: %((freq_n2_ansys-freq_n2)/freq_n2)*100%
    /REPLOT
*END
*ABBR,freq_2,freq_2 ! create a buttom (Toolbar shorcut)

*CREATE,freq_3,mac, ! create a file containing a macro
    /POST1
    SET,,, ,,, ,3  ! vibration mode
    PLNSOL, U,SUM, 1,1.0
    /TITLE, %n_elements% beam elements, error: %((freq_n3_ansys-freq_n3)/freq_n3)*100%
    /REPLOT
*END
*ABBR,freq_3,freq_3 ! create a buttom (Toolbar shorcut)